plugins {
	id "fabric-loom" version "1.6-SNAPSHOT"
	id "org.ajoberstar.grgit" version "5.2.2"
}

version = project.mod_version
description = project.description
group = project.maven_group

repositories {
	// Essential Maven/Minecraft Libraries are auto configured
	maven { url "https://jitpack.io" }
	maven { url "https://api.modrinth.com/maven" }
}

dependencies {
	// Change versions in gradle.properties

	// Minecraft : Client & Yarn Mappings
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"

	// Fabric : API is optional but useful
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Development : IDE Only
	modRuntimeOnly "maven.modrinth:modmenu:9.0.0"
	modRuntimeOnly "maven.modrinth:sodium:mc1.20.4-0.5.8"
	modRuntimeOnly "maven.modrinth:iris:1.7.0+1.20.4"
	modRuntimeOnly "maven.modrinth:nvidium:0.2.6-beta"

	// Third Party : Libraries
	implementation "io.github.nevalackin:radbus:1.0.0"
	implementation 'org.reflections:reflections:0.10.2'
}

def properties = [
	"id": project.name.toLowerCase(),
	"name": project.name,
	"version": project.version,
	"description": project.description,
	"minecraft_version": project.minecraft_version,
	"loader_version": project.loader_version
]

processResources {
	inputs.properties properties
	filteringCharset "UTF-8"

	filesMatching("fabric.mod.json") {
		expand properties
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}

	// Required : Generate Source Remapped
	withSourcesJar()
}